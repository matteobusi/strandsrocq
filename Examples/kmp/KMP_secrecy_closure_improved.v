(*
  In this file we finally prove the secrecy guarantees from
    "Secure Key Management Policies in Strand Spaces" by Focardi and Luccio
  using our improved closure.
*)
Require Import Lia.
Require Import Coq.Lists.List.
Import Coq.Lists.List.ListNotations.
Require Import ListSet.

Require Import DefaultInstances.
Require Import PenetratorSAL.
Require Import RelMinimal.

Require Import KMP_policies.
Require Import KMP_protocol.
Require Import KMP_closure_improved.

Set Implicit Arguments.

(* This section covers the secrecy guarantees in the protocol KMP as induced by a given policy *)
Section secrecy_guarantee.

  (* Assume a given policy Ï€ and its induced strand space Î£ *)
  Variable Ï€ : policy__t.
  Variable C : edge_set__t.

  Hypothesis C_is_bundle : is_bundle C.
  Hypothesis C_is_KMP : C_is_SS C (KMP_StrandSpace Ï€).

  (*
    Terms have initial type.
    If a key k is s.t. K_d k, then its initial type is the type assigned when it originated; Any other term m (incl. keys such that ~ K_d m) have initial type D.
  *)
  Fixpoint initial_type_rec N (m : ð”¸) : KEY_DATA_T :=
    match m, N with
    | #k, n::NL => match tr (strand n) with
                      | [âŠ• âŸ¨ #k' â‹… ($KT) âŸ©_(mk)] =>
                        if K_eq_dec k k' then
                          (if K_d_dec k then
                            (if K_m_dec mk then
                              (KDn KT)
                            else initial_type_rec NL m)
                          else initial_type_rec NL m)
                        else initial_type_rec NL m
                      | _ => initial_type_rec NL m
                      end
    | _, _ => D
    end.
  Definition initial_type := initial_type_rec (nodes_of C).

  Lemma initial_type_char :
    forall m,
      match m with
      | #k =>
        (K_d k ->
          forall n (KT : KEY_T) mk, K_m mk -> is_node_of n C -> [âŠ• âŸ¨ #k â‹… $KT âŸ©_(mk)] = tr (strand n) ->
            initial_type m = (KDn KT)
        ) /\
        (~K_d k -> initial_type m = D)
      | _ => initial_type m = D
      end.
  Proof.
    intros m.
    destruct m eqn:Hm; unfold initial_type, is_node_of in *;
    unfold C_is_SS, is_node_of in C_is_KMP;
    induction (nodes_of C) as [|n' nl IHnl] ; try easy.
    assert (forall n : node__t, set_In n nl -> KMP_StrandSpace Ï€ (strand n)) as C_is_KMP' by (st_implication C_is_KMP).
    split.
    - intros Hd n KT mk Hmk Hin Htr. destruct Hin as [Hneq | Hin].
      + subst. unfold initial_type_rec. rewrite <- Htr. destruct (K_eq_dec k k) as [? | ?]; destruct (K_d_dec k) as [_ | Hnk]; destruct (K_m_dec mk) as [_ | Hnmk]; try easy.
      + destruct (IHnl C_is_KMP') as [IHnld _]. specialize (IHnld Hd n KT mk Hmk Hin Htr) as IHnldn. simpl.
        destruct (tr (strand n')) as [|s0 sl] eqn:Htrn'; try easy.
        destruct s0 as [a|a]; try easy.
        destruct a; try easy.
        destruct a; try easy.
        destruct a1 eqn:Ha1; destruct a2; try easy.
        destruct sl; try easy.
        destruct (K_eq_dec k k1) as [? | ?]; destruct (K_d_dec k1) as [Hk1 | ?]; destruct (K_d_dec k) as [Hk | ?]; destruct (K_m_dec k0) as [Hk0 | ?]; try inversion e as [e']; try rewrite <- e' in *; try easy.
        (* This last case is a contradiction *)
        specialize (C_is_KMP n) as Hn_kmp; specialize (C_is_KMP n') as Hn'_kmp. symmetry in Ha1, e'.
        st_implication Hn_kmp; st_implication Hn'_kmp.
        inversion Hn_kmp as [s0 Hreg |s1 Hpen].
        * inversion Hn'_kmp as [s0' Hreg' |s1' Hpen'].
          --  (* TODO: cleanup this part *)
              assert (originates #k (strand n, 0)) as Horig.
              {
                unfold originates. repeat split.
                - unfold is_positive, term. simpl. rewrite <- Htr. now simpl.
                - unfold occurs, uns_term, term. simpl. rewrite <- Htr. simpl. intuition.
                - intros n0 Hintra. specialize (intrastrand_index_lt Hintra) as Hlt. simpl in Hlt. lia.
              }
              assert (originates #k (strand n', 0)) as Horig'.
              {
                unfold originates. repeat split.
                - unfold is_positive, term. simpl. rewrite Htrn'. now simpl.
                - unfold occurs, uns_term, term. simpl. rewrite Htrn'. simpl. intuition.
                - intros n0 Hintra. specialize (intrastrand_index_lt Hintra) as Hlt. simpl in Hlt. lia.
              }
              assert (strand n = strand n') as Heqnn'.
              {
                inversion Hreg as [kr mkr KTr [_ [_ [n'' [Horign'' Heq]]]]|?|?|?|?];
                subst; try (now rewrite <-H2 in Htr).

                inversion Htr. inversion Htrn'; subst.
                rewrite H1.
                apply (f_equal tr) in H1; simpl in H1; rewrite <-H0 in H1; inversion H1; subst.
                specialize (Heq (strand n, 0) Horig) as Heqn; specialize (Heq (strand n', 0) Horig') as Heqn'.
                rewrite Heqn in Heqn'. now inversion Heqn'.
              }
              rewrite Heqnn' in Htr; rewrite Htrn' in Htr; now inversion Htr.
          -- inversion Hpen' as [t0 i Htrace|g i Htrace|g i Htrace|g h i Htrace|g h i Htrace|k' i Hpenkey Htrace|k' h i Htrace|k' h i Htrace]; apply (f_equal tr) in Htrace; simpl in Htrace; now rewrite <- Htrace in Htrn'.
        * inversion Hpen as [t0 i Htrace|g i Htrace|g i Htrace|g h i Htrace|g h i Htrace|k' i Hpenkey Htrace|k' h i Htrace|k' h i Htrace]; apply (f_equal tr) in Htrace; simpl in Htrace; now rewrite <- Htrace in Htr.
    - intros Hnk.
      destruct (IHnl C_is_KMP') as [_ IHnlnd]. specialize (IHnlnd Hnk). simpl.
      destruct (tr (strand n')) as [|s0 sl] eqn:Htrn'; try easy.
      destruct s0 as [a|a]; try easy.
      destruct a; try easy.
      destruct a; try easy.
      destruct a1 eqn:Ha1; destruct a2; try easy.
      destruct sl; try easy.
      destruct (K_eq_dec k k1) as [? | ?]; destruct (K_d_dec k1) as [Hk1 | ?]; destruct (K_d_dec k) as [Hk | ?]; destruct (K_m_dec k0) as [Hk0 | ?]; try inversion e as [e']; try rewrite <- e' in *; try easy.
  Qed.

  (* This lemma and the following corollary are useful if we want to prove
     security with terms supporting asymmetric ciphers *)
  Lemma initial_type_symm_K_d :
    forall k, (K_d k /\ k = k â»Â¹) \/ (~K_d k /\ initial_type # k = initial_type # k â»Â¹).
    Proof.
      intros k.
      destruct (K_d_dec k).
      - left. destruct k; easy.
      - right.
        assert (~ K_d k â»Â¹) by ( unfold not, inv; intros; destruct k; try easy ).
        specialize (initial_type_char # k â»Â¹) as Hchar; simpl in Hchar.
        st_implication Hchar. clear Hand. st_implication Hand0.
        specialize (initial_type_char # k) as Hchar; simpl in Hchar;
        st_implication Hchar; clear Hand. st_implication Hand1.
        now rewrite <-Hand0 in Hand1.
    Qed.
  Corollary initial_type_symm :
    forall k, initial_type # k = initial_type # k â»Â¹.
  Proof.
    intros. specialize (initial_type_symm_K_d k) as [[_ Hd]|[_ Hnd]]; try easy.
    now rewrite <-Hd.
  Qed.

  Lemma create_implies_initial_type :
      forall k (KT : KEY_T) mk m,
        K_m mk -> K_d k -> is_node_of m C ->
          [âŠ• âŸ¨ #k â‹… $KT âŸ©_(mk)] = tr (strand m) ->
            initial_type #k = (KDn KT).
  Proof.
    intros k KT mk m Hmk Hk Hisnode Htr.
    specialize (initial_type_char #k) as Hit.
    simpl in Hit; destruct Hit as [Hd Hnd].
    apply (Hd Hk m KT mk Hmk Hisnode Htr).
  Qed.

  (* Assume now to have a clousure Ï• and a reachable relation â„œ, satisfying is_closure *)
  Variable Ï• : cl_policy__t.
  Variable â„œ : reachable__t.
  Definition Ï•_â„œ_closes_Ï€ := is_closure Ï€ (Ï•, â„œ).

  Definition wf_cipher_master k KT := (Ï•, â„œ) âŠ¢ KDn KT âˆˆ initial_type k.
  Lemma wf_cipher_master_dec : forall k KT, {(Ï•, â„œ) âŠ¢ KDn KT âˆˆ initial_type k } + {~(Ï•, â„œ) âŠ¢ KDn KT âˆˆ initial_type k}.
  Proof.
    intros k KT;
    apply set_In_dec; repeat decide equality; repeat apply U_eq_dec.
  Qed.

  Definition P_wcnmb k k' el :=
    snd (fst el) = Enc /\ set_In (snd el, initial_type k) â„œ /\ set_In (fst (fst el), initial_type k') â„œ.

  Lemma P_wcnmb_dec : forall k k' x, {P_wcnmb k k' x} + {~ P_wcnmb k k' x}.
  Proof.
    intros k k' x.
    unfold P_wcnmb.
    destruct x as [[KT' e] KT]; simpl in *.
    destruct e; try now right.
    destruct (set_In_dec reachable__t_el_eq_dec (KT, initial_type k) â„œ); try tauto.
    destruct (set_In_dec reachable__t_el_eq_dec (KT', initial_type k') â„œ); try tauto.
  Qed.

  Definition wf_cipher_nonmaster k k' :=
    exists KT KT',
          (Ï•, â„œ) âŠ¢ KT' =[Enc]=> KT /\
          ((Ï•, â„œ) âŠ¢ KT âˆˆ initial_type k) /\
          ((Ï•, â„œ) âŠ¢ KT' âˆˆ initial_type k').

  Lemma wcnmb_iff_wcnm : forall k k', Exists (P_wcnmb k k') Ï• <-> wf_cipher_nonmaster k k'.
  Proof.
    intros k k'.
    unfold wf_cipher_nonmaster.
    rewrite Exists_exists.
    split.
    - intro Hb. destruct Hb as [[[KT' e] KT] [Hin [Hit Hit']]].
      simpl in *. subst. exists KT, KT'. intuition.
    - intro H. destruct H as [KT [KT' [Henc [Hit Hit']]]].
      exists (KT', Enc, KT). unfold P_wcnmb. intuition.
  Qed.

  Lemma wf_cipher_nonmaster_dec : forall k k', {wf_cipher_nonmaster k k'} + {~ wf_cipher_nonmaster k k'}.
  Proof.
    intros k k'.
    specialize (Exists_dec (P_wcnmb k k') Ï• (P_wcnmb_dec k k')) as Hdec.
    destruct Hdec as [HE|HnE].
    1: left. 2: right.
    all: now rewrite <-wcnmb_iff_wcnm.
  Qed.

  Definition isMK k := match k with | #k' => K_m k' | _ => False end.

  Definition isDK k := match k with | #k' => K_d k' | _ => False end.

  Lemma isDK_dec : forall k, { isDK k } + { ~isDK k }.
  Proof.
    intros k.
    unfold isDK.
    destruct k; try tauto.
    destruct (K_d_dec k); try tauto.
  Qed.

  (* Recall that this should imply that if a term t is accepted by the predicate, then k <> t with K_d k *)
  Fixpoint protected (t : ð”¸) : Prop :=
    match t with
    | âŸ¨ k â‹… KT âŸ©_(mk) =>
      match KT with
        | $KT' =>
            (isMK #mk -> wf_cipher_master k KT') /\
            (~isMK #mk -> wf_cipher_nonmaster k #mk /\ wf_cipher_nonmaster KT #mk) /\
            (~isDK k -> protected k /\ protected KT)
        | _ => protected k /\ protected KT
      end
    | âŸ¨ k âŸ©_(k') =>
        (wf_cipher_nonmaster k #k') /\ (~isDK k -> protected k)
    | g â‹… h => protected g /\ protected h
    | _ => (Ï•, â„œ) âŠ¢ D âˆˆ (initial_type t)
  end.

  Lemma protected_dec : forall t, {protected t} + { ~protected t}.
  Proof.
    intro t.
    induction t as [ | t | k | k t IHt | t1 IHt1 t2 IHt2 ].
    - simpl; destruct (set_In_dec reachable__t_el_eq_dec (D, initial_type Ï„) â„œ); try tauto.
    - simpl; destruct (set_In_dec reachable__t_el_eq_dec (D, initial_type $t) â„œ); try tauto.
    - simpl; destruct (set_In_dec reachable__t_el_eq_dec (D, initial_type #k) â„œ); try tauto.
    - simpl. destruct t as [ | t | k' | k' t | t1 t2 ].
      + destruct (wf_cipher_nonmaster_dec Ï„ #k); destruct (set_In_dec reachable__t_el_eq_dec (D, initial_type Ï„) â„œ); try tauto.
      + destruct (wf_cipher_nonmaster_dec $t #k); destruct (set_In_dec reachable__t_el_eq_dec (D, initial_type $t) â„œ); try tauto.
      + destruct (wf_cipher_nonmaster_dec #k' #k); destruct (set_In_dec reachable__t_el_eq_dec (D, initial_type #k') â„œ); destruct (K_d_dec k'); try tauto.
      + destruct (wf_cipher_nonmaster_dec (âŸ¨ t âŸ©_ k') #k); destruct (isDK_dec (âŸ¨ t âŸ©_ k')); destruct IHt; try tauto.
      + simpl in IHt.
        destruct t2 as [ | t2 | k2 | k2 t2 | t21 t22 ]; try tauto.
        destruct IHt as [[Hp1 Hin] | Hnp ].
        * destruct (K_m_dec k);
          destruct (wf_cipher_master_dec t1 t2);
          destruct (wf_cipher_nonmaster_dec t1 #k);
          destruct (wf_cipher_nonmaster_dec $t2 #k);
          destruct (isDK_dec t1); try tauto.
        * destruct (K_m_dec k);
          destruct (wf_cipher_master_dec t1 t2);
          destruct (wf_cipher_nonmaster_dec t1 #k);
          destruct (wf_cipher_nonmaster_dec $t2 #k);
          destruct (isDK_dec t1); try tauto.
    - simpl. destruct (IHt1); destruct (IHt2); try tauto.
  Qed.

  Definition KMP_p (t : ð”¸) : Prop := ~protected t.

  (* We prove decidability of KMP_p based on protected decidability *)
  Lemma KMP_p_dec : forall t, { KMP_p t } + { ~KMP_p t}.
  Proof.
    intros t.
    specialize (protected_dec t) as Hpd.
    intuition.
  Qed.

  Definition N_KMP := N KMP_p C KMP_p_dec.
  Definition N_iff_inC_p_KMP := N_iff_inC_p KMP_p C KMP_p_dec.
  Definition minimal_N_KMP_then_mpt := minimal_N_then_mpt KMP_p C_is_bundle KMP_p_dec.

  Lemma no_minimal_is_regular :
    Ï•_â„œ_closes_Ï€ ->
      forall m,
        set_In m N_KMP -> is_minimal (bundle_le C) m N_KMP -> ~KMP_strand Ï€ (strand m).
  Proof.
    intros Ï•_â„œ_closes_Ï€ m Hin Hmin Hreg.
    specialize initial_type_char as Hit.
    assert (Hin':=Hin); apply N_iff_inC_p_KMP in Hin' as [Hinm HpKMP].

    specialize Ï•_â„œ_closes_Ï€ as Hub_clos. destruct Hub_clos as [Hcl [Hcl_refl [Hcl_D [Hcl_ed [Hcl_e Hcl_d]]]]].

    inversion Hreg as [k mk KT [Hk Hmk] i Htr | k mk KT m' [Hk Hmk] i Htr | k mk KT m' [Hk Hmk] i Htr| k1 k2 mk KT1 KT2 [Hk Hmk] i Htr | k1 k2 mk KT1 KT2 [Hk Hmk] i Htr]; apply (f_equal tr) in Htr; simpl in Htr.
    all: specialize (minimal_N_KMP_then_mpt Htr Hin Hmin) as Hmpti; unfold pred in *; unfold KMP_p in Hmpti; simpl in Hmpti.
    all: simplify_prop in Hmpti.
    - (* Create case *)
      unfold wf_cipher_master, isDK, isMK in *.
      specialize (create_implies_initial_type (k:=k) (KT:=KT) (mk:=mk) m) as Hci. st_implication Hci.
      rewrite Hci in Hand. st_implication Hand.
    - (* Encrypt case *)
      (* simplify_prop in Hmpti. *)
      unfold wf_cipher_master, wf_cipher_nonmaster in *.
      st_implication Hand0.
      apply Hand3. split; try easy. exists D, KT. repeat split; try easy. now apply Hcl.
    - (* Wrap case *)
      (* simplify_prop in Hmpti. *)
      unfold wf_cipher_master, wf_cipher_nonmaster in *.
      st_implication Hand1. st_implication Hand0.
      apply Hand5. split; try easy. exists KT1, KT2. repeat split; try easy. now apply Hcl.
    - (* Unwrap case *)
      (* simplify_prop in Hmpti. *)
      apply Hand4.
      unfold wf_cipher_master, wf_cipher_nonmaster in *.
      st_implication Hand0.
      split; try easy; intros _.
      destruct Hand1 as [KT1' [KT2' [Henc [Hk1 Hk2]]]].
      specialize (Hcl_e KT2' KT1' (initial_type #k2) (initial_type #k1)) as Hit12; st_implication Hit12.
      specialize (Hcl_d KT2 KT1 (initial_type #k2)) as Hdec.
      specialize (Hcl KT2 Dec KT1) as Hcl'. st_implication Hdec.
      now apply (Hcl_ed (initial_type #k2) (initial_type #k1) KT1).
  Qed.

  Lemma KMP_never_originates_mk :
    forall k, K_m k -> never_originates_regular K__P_md k C.
  Proof.
    intros k0 Hkm n Hnodeof Horig.
    unfold not.
    unfold isMK in *.
    specialize (C_is_KMP n Hnodeof) as Hn_kmp.
    inversion Hn_kmp as [s Hreg|s Hpen]; try easy.
    (* unfold KMP_strand in Hreg; *)
    inversion Hreg as [k mk KT [Hk Hmk] i Htr | k mk KT m' [Hk Hmk] i Htr | k mk KT m' [Hk Hmk] i Htr| k1 k2 mk KT1 KT2 [Hk Hmk] i Htr | k1 k2 mk KT1 KT2 [Hk Hmk] i Htr]; apply (f_equal tr) in Htr; simpl in Htr;
    apply (originates_then_mpt Htr) in Horig; unfold mpt in Horig; simpl in Horig;
    simplify_prop in Horig.
    specialize (K_d_then_not_K_m Hk) as Hkdnkm; try easy.
  Qed.

  Lemma no_minimal_is_penetrator :
    Ï•_â„œ_closes_Ï€ ->
      forall m,
        set_In m N_KMP -> is_minimal (bundle_le C) m N_KMP -> ~penetrator_node K__P_md m.
  Proof.
    intros Ï•_â„œ_closes_Ï€ m Hin Hmin Hpen.
    (* specialize trace_of_s as Hstr.
    specialize C_is_bundle as Hbundle. *)
    specialize initial_type_char as Hit.
    assert (Hin':=Hin); apply N_iff_inC_p_KMP in Hin' as [Hinm HpKMP].
    specialize Ï•_â„œ_closes_Ï€ as Hub_clos. destruct Hub_clos as [Hcl Hub].

    (* unfold penetrator_node, penetrator_strand in Hpen.
    assert (Hpentrace:=Hpen); apply is_penetrator in Hpentrace.
    inversion Hpentrace as [t Htrace|g Htrace|g Htrace|g h Htrace|g h Htrace|k Hpenkey Htrace|k h Htrace|k h Htrace]. *)
    inversion Hpen as [t i Htrace|g i Htrace|g i Htrace|g h i Htrace|g h i Htrace|k i Hpenkey Htrace|k h i Htrace|k h i Htrace]; apply (f_equal tr) in Htrace; simpl in Htrace.

    all: specialize (minimal_N_KMP_then_mpt Htrace Hin Hmin) as Hmpti; unfold pred in *; unfold KMP_p in Hmpti; simpl in Hmpti.
    all: simplify_prop in Hmpti. (* Automatically solves: flush, tee, concatenation, separation *)
    - (* Text case *)
      apply Hand. specialize (Hit $t); simpl in Hit. now rewrite Hit in *.
    - (* Penetrator key case *)
      apply Hand. specialize (Hit #k); simpl in Hit; destruct Hit as [_ Hnkd].
      apply (K__P_md_then_not_K_d) in Hpenkey.
      st_implication Hnkd; now rewrite Hnkd.
    - (* Enc case *)
      destruct h as [|?|?|?|?].
      all: try (unfold wf_cipher_master, wf_cipher_nonmaster in *; simpl in *; apply Hand0; split; try easy; exists D, D; repeat split; try easy).
      + now specialize (Hit (âŸ¨ h âŸ©_ k0)); simpl in Hit; rewrite Hit.
      + unfold wf_cipher_master, wf_cipher_nonmaster in *; simpl in *.
        destruct h2 as [|KT'|?|?|?]; try easy.
        apply Hand0; clear Hand0; repeat split; try easy.
        * intros Hkm.
          specialize (index_lt_strand_implies_is_node_of C_is_bundle (strand m, 0) m) as Hisnode; st_implication Hisnode.
          unfold K__P_md in Hkm.
          apply K_m_then_not_K__P_md in Hkm as Hknkp.
          specialize (penetrator_bound C_is_bundle Hknkp (KMP_never_originates_mk Hkm) Hisnode) as Hnsub. st_implication Hnsub.
          (* simplify_term_in Hnsub. *)
        * intros. exists D, D. repeat split; try easy.
          destruct h1 eqn:Heqh1.
          all: (specialize (Hit h1); rewrite Heqh1 in Hit; simpl in Hit; try rewrite Hit; try easy).
        * intros. exists D, D. repeat split; try easy.
    - (* Dec case *)
      destruct Hub as [Hrefl [[HDdec HDenc] [Hreach [He_enc He_dec]]]].
      destruct h as [|h|k0|k0 h|h1 h2].
      all: try tauto.
      + destruct Hand1 as [[KT [KT' [Henc [HKT HKT']]]] _].
        specialize (initial_type_symm k) as Hsymm; rewrite <-Hsymm in Hand.
        (* unfold inv in Hand; simpl in Hand. *)
        specialize (He_enc KT' KT (initial_type #k) (initial_type #k0)); st_implication He_enc.
        specialize (He_dec D D (initial_type #k)); st_implication He_dec.
        now specialize (Hreach (initial_type #k) (initial_type #k0) D); st_implication Hreach.
      + destruct h1 as [|?|?|k0 a|a1 a2] eqn:Hh1; destruct h2 eqn:Hh2; try (now apply Hand0).
        * (* h1 = Ï„; h2 = $_ *)
          unfold protected, isDK in *. simpl in *.
          specialize (Hit $t) as Hit0; simpl in Hit0.
          specialize (Hit A_Ï„) as Hitt; simpl in Hitt. rewrite Hit0, Hitt in *. now apply Hand0.
        * (* h1 = $_; h2 = $_ *)
          unfold protected, isDK in *. simpl in *.
          assert (Hit0:=Hit $t); simpl in Hit0; assert (Hit1:=Hit $t0); simpl in Hit1; rewrite Hit0, Hit1 in *; now apply Hand0.
        * (* h1 = k0;  h2 = $_ *)
          (* apply H0; intros [HKmk_in Hp5]. destruct (K_m_dec k). *)
          destruct (K_m_dec k) as [Hkm | Hnkm].
          -- specialize (index_lt_strand_implies_is_node_of C_is_bundle (strand m, 0) m) as Hisnode; st_implication Hisnode.
             specialize (K_m_then_not_K__P_md Hkm) as Hknkp.
             specialize (penetrator_bound C_is_bundle Hknkp (KMP_never_originates_mk Hkm) Hisnode) as Hnsub. st_implication Hnsub.
             simplify_term_in Hnsub. inversion Hkm. now rewrite <-H in Hnsub.
          -- simplify_prop in Hand1.
             st_implication Hand1.
             unfold wf_cipher_nonmaster in Hand4, Hand6.
             destruct Hand4 as [kKT [kKT' [Hkenc [HkKT HkKT']]]].
             destruct Hand6 as [tKT [tKT' [Htenc [HtKT HtKT']]]].
             specialize (initial_type_symm k) as Hsymm; rewrite <-Hsymm in Hand.
             specialize (He_enc kKT' kKT (initial_type #k) (initial_type #k0)) as H5kk0; st_implication H5kk0.
             specialize (He_enc tKT' tKT (initial_type #k) (initial_type $t)) as H5kt0; st_implication H5kt0.
             specialize (He_dec D D (initial_type #k)); st_implication He_dec.
             specialize (Hreach (initial_type #k) (initial_type #k0) D) as H3k0; st_implication H3k0.
             specialize (Hreach (initial_type #k) (initial_type $t) D) as H3t0; simpl in Hand0; st_implication H3t0.

        * (* h1 = âŸ¨ a âŸ©_ k0); h2 = $_ *)
          specialize (Hit $t); simpl in Hit.
          st_implication Hand1.
          destruct (K_m_dec k) as [Hkm | Hnkm].
          -- apply Hand0; clear Hand0; try easy.
              st_implication Hand2.
              simpl. tauto.
          -- st_implication Hand1. simpl in Hand0. tauto.
        * (* h1 = h1_1 â‹… h1_2; h2 = $_ *)
          specialize (Hit $t); simpl in Hit.
          st_implication Hand1.
          destruct (K_m_dec k) as [Hkm | Hnkm].
          -- apply Hand0; clear Hand0; try easy.
              st_implication Hand2.
              simpl. tauto.
          -- st_implication Hand1. simpl in Hand0. tauto.
  Qed.

  (* Put everything together: the set N_KMP has no minimal element *)
  Lemma N_KMP_no_minimal :
    Ï•_â„œ_closes_Ï€ ->
      forall m, set_In m N_KMP -> ~is_minimal (bundle_le C) m N_KMP.
  Proof.
    intros Ï•_â„œ_closes_Ï€ m Hin Hmin.
    specialize (C_is_KMP m) as Hn_kmp.
    assert (Hin':=Hin);
    apply (N_iff_inC_p_KMP) in Hin'.
    st_implication Hn_kmp.
    inversion Hn_kmp as [Hreg|Hpen].
    - now specialize (no_minimal_is_regular Ï•_â„œ_closes_Ï€ Hin Hmin) as Hnreg.
    - now specialize (no_minimal_is_penetrator Ï•_â„œ_closes_Ï€ Hin Hmin) as Hnpen.
  Qed.

  (* This is Theorem 1 from the paper *)
  Theorem device_key_secrecy:
    Ï•_â„œ_closes_Ï€ ->
      forall m, is_node_of m C -> protected (uns_term m).
  Proof.
    intros Ï•_â„œ_closes_Ï€ m Hm_in_C.
    destruct (protected_dec (uns_term m)) as [Hprot | Hnprot]; try easy.
    assert (set_In m N_KMP). { apply (N_iff_inC_p_KMP). auto. }
    specialize (N_KMP_no_minimal) as Hnomin.
    assert ({N_KMP = nil} + {N_KMP <> nil}) as N_KMP_empty_dec by (repeat decide equality).
    destruct N_KMP_empty_dec as [Hemp | Hnemp].
    - (* empty *) now rewrite Hemp in H.
    - (* nonempty *)
      specialize (RelMinimal.exists_minimal eq_node__t_dec (bundle_le_dec C) (bundle_le_antisymm C_is_bundle) (bundle_le_trans (C:=C)) Hnemp) as [m' [Hin Hmin]].
      now specialize (Hnomin Ï•_â„œ_closes_Ï€ m' Hin).
  Qed.


  (* This is Theorem 2 (Security) from the paper.
    This gives a necessary condition for policies to be secure: a device key k never appears as plaintext if a D is not reachable from k.
  *)
  Corollary secrecy:
    Ï•_â„œ_closes_Ï€ ->
      forall k m, is_node_of m C ->
          K_d k ->
            ~ (Ï•, â„œ) âŠ¢ D âˆˆ (initial_type #k) ->
              #k <> uns_term m.
  Proof.
    intros Ï•_â„œ_closes_Ï€ k m Hm_in_C Hdev Hreach Heq.
    specialize (device_key_secrecy Ï•_â„œ_closes_Ï€ m Hm_in_C) as Hsub.
    unfold protected in Hsub.
    destruct (uns_term m); rewrite Heq in Hreach; try easy.
  Qed.

End secrecy_guarantee.
